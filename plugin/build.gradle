apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "java-gradle-plugin"
apply plugin: "maven-publish"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.gradle.kotlin.kotlin-dsl"

group = "firebase.test.lab"
version = "2.6.2"

pluginBundle {
    website = "https://github.com/piotrmadry/firebase-test-lab-gradle-plugin"
    vcsUrl = "https://github.com/piotrmadry/firebase-test-lab-gradle-plugin.git"
    tags = ["firebase", "test-lab", "espresso", "instrumental-tests", "kotlin", "android"]
}

gradlePlugin {
    plugins {
        FirebaseTestLabPlugin {
            id = "firebase-test-lab"
            displayName = "Firebase Test Lab Plugin"
            description = "Gradle plugin for Android Test Lab"
            implementationClass = "com.appunite.firebasetestlabplugin.FirebaseTestLabPlugin"
        }
    }
}

tasks {
    test {
        testLogging.showExceptions = true
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin' && requested.name == 'kotlin-reflect') {
            details.useVersion kotlin_version
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.android.tools.build:gradle:3.6.1"
    testImplementation "junit:junit-dep:4.4"
}
